load("//conftest/private:toolchains_repo.bzl", "PLATFORMS", "toolchains_repo")
load("//conftest/private:versions.bzl", "CONFTEST_VERSIONS")

def _conftest_sdk_impl(ctx):
    ctx.report_progress("Downloading and extracting Open Policy Agent executable")

    ctx.download_and_extract(
        url = ctx.attr.urls,
        sha256 = ctx.attr.sha256,
    )

    build_content = """# Generated by conftest/repositories.bzl
load("@ohno_cloud_tools_manifests//conftest:toolchain.bzl", "conftest_toolchain")

conftest_toolchain(
    name = "conftest_toolchain",
    cli = select({
        "@bazel_tools//src/conditions:host_windows": ":conftest.exe",
        "//conditions:default": ":conftest",
    }),
)
"""

    # Base BUILD file for this repository
    ctx.file("BUILD.bazel", build_content)

conftest_sdk = repository_rule(
    implementation = _conftest_sdk_impl,
    attrs = {
        "urls": attr.string_list(mandatory = True),
        "sha256": attr.string(mandatory = True),
    },
)

def conftest_register_toolchains(name, version = "0.62.0", register = True, **kwargs):
    """Convenience macro for users which does typical setup.
    Users can avoid this macro and do these steps themselves, if they want more control.
    Args:
        name: base name for all created repos, like "conftest1_14"
        register: whether to call through to native.register_toolchains.
            Should be True for WORKSPACE users, but false when used under bzlmod extension
        **kwargs: passed to each conftest_sdk call
    """
    version_info = CONFTEST_VERSIONS.get(version)

    for platform in version_info.keys():
        conftest_sdk(
            name = name + "_" + platform,
            **version_info.get(platform)
        )
        if register:
            native.register_toolchains("@%s_toolchains//:%s_toolchain" % (name, platform))

    toolchains_repo(
        name = name + "_toolchains",
        user_repository_name = name,
    )
